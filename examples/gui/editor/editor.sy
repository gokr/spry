#!/usr/bin/env spry

# A sample Spry application that is a trivial file editor.

# First we need to initialize libui
uiInit

# Variable holding last known filepath
path = nil

# Two funcs for opening and saving a file
openContent = func [
  # We use reassignment here ":=" to make sure we reach the outer binding
  path := (win openFile)
  path nil? else: [
    contentEntry text: readFile path
  ]
]
saveContent = func [:path
  writeFile path (contentEntry text)
]

byeBye = func [win message: "Bye bye" title: "Braindead editor"]

# A handler for closing
closeHandler = [
  byeBye
  win destroy
  uiQuit
]

# Build the app menu
menu = newMenu "File"

menu addItem: "Open ..." onClicked: [openContent]
menu addItem: "Save as ..." onClicked: [
  path := (win saveFile)
  path nil? else: [
    saveContent path
  ]
]

menu addQuitItemShouldClose: [do closeHandler ^true]

helpMenu = newMenu "Help"
helpMenu addItem: "Help" onClicked: [
  win message: "Sorry..." title: "No help to get"
]

# Silly About
helpMenu addAboutItemOnClicked: [
  win message: "This is a truly trivial file editor." title: "About Braindead editor"
]

# Create a new app Window
win = newWindow: "Braindead editor" width: 640 height: 400 hasBar: true
win margined: true

# Create a multiline text entry field for content
contentEntry = newMultilineEntryText

# Some boxes put stuff in
layout = newVerticalBox
buttons = newHorizontalBox

# Some buttons and their handlers
saveit = newButton: "Save" onClicked: [
  path nil? then: [
    path := (win saveFile)
  ]
  path nil? else: [
    saveContent path
  ]
]
clearit = newButton: "Clear" onClicked: [contentEntry text: ""]
quitit = newButton: "Quit" onClicked: closeHandler

# Put things in the boxes
layout padded: true
layout add: contentEntry stretch: true
layout add: buttons stretch: false
buttons add: saveit stretch: false
buttons add: clearit stretch: false
buttons add: quitit stretch: false

# Add box to window
win setChild: layout

# Set initial text in text component
contentEntry text: ""

# Set a handler on closing window, this one should not do uiQuit etc
win onClosingShouldClose: [byeBye true]

# Show the window
win show

# Enter libui's event loop
win uiMain