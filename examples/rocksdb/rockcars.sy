#!/usr/bin/env spry

# Create a database and get a connection to it
db = openRocksDB "cars.db"

# Utility funcs
saveCar = func [:car db rockAt: car::id put: car]
loadCar = func [db rockAt: :id]
deleteCar = func [db deleteString: (compress serialize :id)]

# Create two cars and store separately
car1 = {id = 0 name = "Saab"}
car2 = {id = 1 name = "Volvo"}

# Store them
echo "Storing..."
echo ("Saving car 0: ", (car1 print))
saveCar car1

echo ("Saving car 1: ", (car2 print))
saveCar car2

# Read and show
car = loadCar 1
echo ("Loaded car 1: ", (car print))

# Modify and save back
car::name = "Toyota"
saveCar car
echo ("Modified car 1: ", (car print))

# Load again
another = loadCar 1
echo ("Loaded car 1 again: ", (another print))

# Close db
db closeRocksDB
