#!/usr/bin/env spry

# Initialize libui
uiInit

# Create a new Window
win = newWindow "Spry Says Hi to ESUG 2016!" 200 200 true

# Create a multiline text entry field for code
code = newMultilineEntryText

# And one for output
out = newMultilineEntryText

# And a vertical box to put stuff in
layout = newVerticalBox
buttons = newHorizontalBox

# Function to parse and evaluate code
run = func [do parse ("[", (code text), "]")]

# Some buttons and their handlers
doit = newButton "Do it"
doit onClicked: [run]

printit = newButton "Print it"
printit onClicked: [
  # Append result to out text field
  out append: ("\x0A", (run print))
]

clearit = newButton "Clear"
clearit onClicked: [out text: ""]

quitit = newButton "Quit"
quitit onClicked: [quit 0]

# Put things in the boxes
layout padding: 0
layout append: code stretch: 1
layout append: out stretch: 1
layout append: buttons stretch: 0
buttons append: doit stretch: 0
buttons append: printit stretch: 0
buttons append: clearit stretch: 0
buttons append: quitit stretch: 0

# Add box to window
win setChild: layout

# Set initial text
code text: "3 + 4"

# Set a handler on closing window
win onClosing: [
  win message: "Thanks for trying it out!" title: "Spry native cross-platform IDE for Windows, OSX and Unix!"
  controlDestroy win
  uiQuit
]

# Show the window
win show

# Enter libui's event loop
win uiMain
